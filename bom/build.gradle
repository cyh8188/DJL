plugins {
    id "java-platform"
    id "maven-publish"
    id 'signing'
}

group = "ai.djl"
boolean isRelease = project.hasProperty("release") || project.hasProperty("staging")
version = "${djl_version}" + (isRelease ? "" : "-SNAPSHOT")

dependencies {
    constraints {
        api "ai.djl:api:${version}"
        api "ai.djl:basicdataset:${version}"
        api "ai.djl:model-zoo:${version}"
        api "ai.djl:serving:${version}"
        api "ai.djl.android:core:${version}"
        api "ai.djl.android:onnxruntime:${version}"
        api "ai.djl.android:pytorch-native:${version}"
        api "ai.djl.audio:audio:${version}"
        api "ai.djl.aws:aws-ai:${version}"
        api "ai.djl.fasttext:fasttext-engine:${version}"
        api "ai.djl.hadoop:hadoop:${version}"
        api "ai.djl.huggingface:tokenizers:${version}"
        api "ai.djl.ml.lightgbm:lightgbm:${version}"
        api "ai.djl.ml.xgboost:xgboost-gpu:${version}"
        api "ai.djl.ml.xgboost:xgboost:${version}"
        api "ai.djl.mxnet:mxnet-engine:${version}"
        api "ai.djl.mxnet:mxnet-model-zoo:${version}"
        api "ai.djl.mxnet:mxnet-native-cu102mkl:linux-x86_64:${mxnet_version}"
        api "ai.djl.mxnet:mxnet-native-cu112mkl:linux-x86_64:${mxnet_version}"
        api "ai.djl.mxnet:mxnet-native-mkl:linux-x86_64:${mxnet_version}"
        api "ai.djl.mxnet:mxnet-native-mkl:osx-x86_64:${mxnet_version}"
        api "ai.djl.mxnet:mxnet-native-mkl:win-x86_64:${mxnet_version}"
        api "ai.djl.onnxruntime:onnxruntime-engine:${version}"
        api "ai.djl.opencv:opencv:${version}"
        api "ai.djl.paddlepaddle:paddlepaddle-engine:${version}"
        api "ai.djl.paddlepaddle:paddlepaddle-model-zoo:${version}"
        api "ai.djl.paddlepaddle:paddlepaddle-native-cpu:linux-x86_64:${paddlepaddle_version}"
        api "ai.djl.paddlepaddle:paddlepaddle-native-cpu:osx-x86_64:${paddlepaddle_version}"
        api "ai.djl.paddlepaddle:paddlepaddle-native-cpu:win-x86_64:${paddlepaddle_version}"
        api "ai.djl.paddlepaddle:paddlepaddle-native-cu102:linux-x86_64:${paddlepaddle_version}"
        api "ai.djl.paddlepaddle:paddlepaddle-native-cu110:win-x86_64:${paddlepaddle_version}"
        api "ai.djl.paddlepaddle:paddlepaddle-native-cu112:linux-x86_64:${paddlepaddle_version}"
        api "ai.djl.python:python:${version}"
        api "ai.djl.pytorch:pytorch-engine:${version}"
        api "ai.djl.pytorch:pytorch-jni:${pytorch_version}-${version}"
        api "ai.djl.pytorch:pytorch-model-zoo:${version}"
        api "ai.djl.pytorch:pytorch-native-cpu-precxx11:linux-aarch64:${pytorch_version}"
        api "ai.djl.pytorch:pytorch-native-cpu-precxx11:linux-x86_64:${pytorch_version}"
        api "ai.djl.pytorch:pytorch-native-cpu:linux-x86_64:${pytorch_version}"
        api "ai.djl.pytorch:pytorch-native-cpu:osx-aarch64:${pytorch_version}"
        api "ai.djl.pytorch:pytorch-native-cpu:osx-x86_64:${pytorch_version}"
        api "ai.djl.pytorch:pytorch-native-cpu:win-x86_64:${pytorch_version}"
        api "ai.djl.pytorch:pytorch-native-cu102:linux-x86_64:${pytorch_version}"
        api "ai.djl.pytorch:pytorch-native-cu117-precxx11:linux-x86_64:${pytorch_version}"
        api "ai.djl.pytorch:pytorch-native-cu117:linux-x86_64:${pytorch_version}"
        api "ai.djl.pytorch:pytorch-native-cu117:win-x86_64:${pytorch_version}"
        api "ai.djl.sentencepiece:sentencepiece:${version}"
        api "ai.djl.spark:spark:${version}"
        api "ai.djl.tablesaw:tablesaw:${version}"
        api "ai.djl.tensorflow:tensorflow-api:${version}"
        api "ai.djl.tensorflow:tensorflow-engine:${version}"
        api "ai.djl.tensorflow:tensorflow-model-zoo:${version}"
        api "ai.djl.tensorflow:tensorflow-native-cpu:linux-x86_64:${tensorflow_version}"
        api "ai.djl.tensorflow:tensorflow-native-cpu:osx-x86_64:${tensorflow_version}"
        api "ai.djl.tensorflow:tensorflow-native-cpu:win-x86_64:${tensorflow_version}"
        api "ai.djl.tensorflow:tensorflow-native-cu113:linux-x86_64:${tensorflow_version}"
        api "ai.djl.tensorflow:tensorflow-native-cu113:win-x86_64:${tensorflow_version}"
        api "ai.djl.tensorrt:tensorrt:${version}"
        api "ai.djl.tflite:tflite-engine:${version}"
        api "ai.djl.tflite:tflite-native-cpu:linux-x86_64:${tflite_version}"
        api "ai.djl.tflite:tflite-native-cpu:osx-x86_64:${tflite_version}"
        api "ai.djl.timeseries:timeseries:${version}"
        api "com.microsoft.onnxruntime:onnxruntime:${onnxruntime_version}"
        api "com.microsoft.onnxruntime:onnxruntime_gpu:${onnxruntime_version}"
    }
}

project.tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

signing {
    required(project.hasProperty("staging") || project.hasProperty("snapshot"))
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

publishing {
    publications {
        create("bom", MavenPublication) {
            from(components["javaPlatform"])

            pom {
                name = "DJL Bill of Materials (BOM)"
                description = "Deep Java Library (DJL) Bill of Materials (BOM)"
                url = "http://www.djl.ai/bom"
                packaging = "pom"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                scm {
                    connection = "scm:git:git@github.com:deepjavalibrary/djl.git"
                    developerConnection = "scm:git:git@github.com:deepjavalibrary/djl.git"
                    url = "https://github.com/deepjavalibrary/djl"
                    tag = "HEAD"
                }

                developers {
                    developer {
                        name = "DJL.AI Team"
                        email = "djl-dev@amazon.com"
                        organization = "Amazon AI"
                        organizationUrl = "https://amazon.com"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            if (project.hasProperty("snapshot")) {
                name = "snapshot"
                url = "https://oss.sonatype.org/content/repositories/snapshots/"
                credentials {
                    username = findProperty("ossrhUsername")
                    password = findProperty("ossrhPassword")
                }
            } else if (project.hasProperty("staging")) {
                name = "staging"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = findProperty("ossrhUsername")
                    password = findProperty("ossrhPassword")
                }
            } else {
                name = "local"
                url = "build/repo"
            }
        }
    }
}
