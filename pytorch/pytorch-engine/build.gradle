group "ai.djl.pytorch"

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    api project(":api")

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
    testImplementation "org.slf4j:slf4j-simple:${slf4j_version}"
    testRuntimeOnly "ai.djl.pytorch:pytorch-native-auto:${pytorch_version}"
}

processResources {
    outputs.dir file("${project.buildDir}/jnilib")
    doLast {
        def url = "https://djl-ai.s3.amazonaws.com/publish/pytorch-${pytorch_version}/jnilib/${djl_version}"
        def files = [
                "linux-x86_64/cpu/libdjl_torch.so"         : "linux-x86_64/cpu/libdjl_torch.so",
                "linux-x86_64/cu101/libdjl_torch.so"       : "linux-x86_64/cu101/libdjl_torch.so",
                "linux-x86_64/cu102/libdjl_torch.so"       : "linux-x86_64/cu102/libdjl_torch.so",
                "precxx11/linux-x86_64/cpu/libdjl_torch.so": "linux-x86_64/cpu-precxx11/libdjl_torch.so",
                "osx-x86_64/cpu/libdjl_torch.dylib"        : "osx-x86_64/cpu/libdjl_torch.dylib",
                "win-x86_64/cpu/djl_torch.dll"             : "win-x86_64/cpu/djl_torch.dll",
                "win-x86_64/cu101/djl_torch.dll"           : "win-x86_64/cu101/djl_torch.dll",
                "win-x86_64/cu102/djl_torch.dll"           : "win-x86_64/cu102/djl_torch.dll"
        ]
        def classesDir = "${project.buildDir}/jnilib"
        files.each { entry ->
            project.logger.lifecycle("Downloading ${url}/${entry.key}")
            def file = new File("${classesDir}/${entry.value}")
            if (file.exists()) {
                project.logger.lifecycle("prebuilt or cached file found for ${entry.value}")
            } else {
                file.getParentFile().mkdirs()
                new URL("${url}/${entry.key}").withInputStream { i -> file.withOutputStream { it << i } }
            }
        }
        // write properties
        def propFile = file("${classesDir}/pytorch.properties")
        propFile.text = "version=${version}\n"

        copy {
            from classesDir
            into "${project.buildDir}/classes/java/main/jnilib"
        }
    }
}

test {
    environment "PATH", "src/test/bin:${environment.PATH}"
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "DJL Engine Adapter for PyTorch"
                description = "Deep Java Library (DJL) Engine Adapter for PyTorch"
                url = "http://www.djl.ai/pytorch/${project.name}"
            }
        }
    }
}
