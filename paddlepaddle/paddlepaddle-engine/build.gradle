group "ai.djl.paddlepaddle"

repositories {
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

processResources {
    def outputDir = file("${project.projectDir}/src/main/include")
    doLast {
        def url = "https://raw.githubusercontent.com/PaddlePaddle/Paddle/v${paddlepaddle_version}/paddle/fluid/inference/capi/paddle_c_api.h"
        project.logger.lifecycle("Downloading ${url}")
        def file = new File("${outputDir}/paddle_c_api.h")
        if (!file.exists()) {
            file.getParentFile().mkdirs()
            new URL(url).withInputStream { i -> file.withOutputStream { it << i } }
        }
    }
}

dependencies {
    api project(":api")

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
    testImplementation "org.slf4j:slf4j-simple:${slf4j_version}"
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "DJL Engine Adapter for PaddlePaddle"
                description = "Deep Java Library (DJL) Engine Adapter for PaddlePaddle"
                url = "http://www.djl.ai/paddlepaddle/${project.name}"
            }
        }
    }
}
