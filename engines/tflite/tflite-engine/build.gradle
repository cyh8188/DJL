group "ai.djl.tflite"

dependencies {
    api project(":api")
    implementation "ai.djl.tflite:tflite-native-auto:${tflite_version}"

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }

    testImplementation(project(":testing"))
    testRuntimeOnly "org.slf4j:slf4j-simple:${slf4j_version}"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "tflite-engine"
            pom {
                name = "DJL Engine Adapter for TFLite"
                description = "Deep Java Library (DJL) Engine Adapter for TFLite"
                url = "http://www.djl.ai/engines/tflite/${project.name}"

                withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies."*".findAll() {
                        it.artifactId.text().startsWith("tflite-native-auto")
                    }.each() {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }
}
