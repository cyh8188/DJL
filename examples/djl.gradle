/**
 * This build file is used by Deep Java Library (DJL) project internally.
 */

plugins {
    id 'application'
}

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    implementation "commons-cli:commons-cli:${commons_cli_version}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_slf4j_version}"
    implementation project(":basicdataset")
    implementation project(":model-zoo")

    if (System.getProperty("ai.djl.default_engine") == "PyTorch") {
        runtimeOnly project(":pytorch:pytorch-model-zoo")
        runtimeOnly "ai.djl.pytorch:pytorch-native-auto:${pytorch_version}-SNAPSHOT"
    } else if (System.getProperty("ai.djl.default_engine") == "TensorFlow") {
        runtimeOnly project(":tensorflow:tensorflow-model-zoo")
        runtimeOnly project(":tensorflow:tensorflow-native-auto")
    } else {
        runtimeOnly project(":mxnet:mxnet-model-zoo")
        runtimeOnly "ai.djl.mxnet:mxnet-native-auto:${mxnet_version}-SNAPSHOT"
    }

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom.withXml {
                // remove mxnet-native-XXX from pom file.
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.artifactId.text().startsWith("mxnet-native-")
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
}

application {
    mainClassName = System.getProperty("main", "ai.djl.examples.inference.ObjectDetection")
}

run {
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperty("file.encoding", "UTF-8")
}

tasks.distTar.enabled = false
